# set shortcut to dotfiles path
export DOTFILES=$HOME/.dotfiles

# set project folder so we can 'c [tab]' to it
export PROJECTS=$HOME/projects

# get all zsh files
config_files=($DOTFILES/**/*.zsh)
typeset -U config_files

# load all path files
for file in ${(M)config_files:#*/path.zsh} ; do
  source $file
done

# load everything but path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh} ; do
  . $file
done

# load private or per-system settings
if [ -f $HOME/.localrc ] ; then
  . $HOME/.localrc
fi

# initialize autocomplete
autoload -Uz compinit
compinit -i

# load completion files after autocomplete
for file in ${(M)config_files:#*/completion.zsh} ; do
  . $file
done

# remove config_files variable now that we're done
unset config_files

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
